
Macro DeployGov
    SetBlockNumber 1
    SetTime 10
    Counter Deploy CNT1
    Comp Deploy Bank
    Enfranchise Root 400001e18
    Timelock Deploy Jared 604800
    Governor Deploy Alpha Alpha (Address Timelock) (Address Comp) Guardian
    Timelock SetAdmin (Address Alpha)
    Assert Equal (Timelock Admin) (Address Alpha)

Macro SucceedQueueExecuteLastProposal gov
    MineBlock
    Governor gov Proposal LastProposal Vote For
    AdvanceBlocks 20000
    MineBlock
    Enfranchise Root 600001e18
    Assert Equal ("Succeeded") (Governor gov Proposal LastProposal State)
    Governor gov Proposal LastProposal Queue
    Assert Equal ("Queued") (Governor gov Proposal LastProposal State)
    IncreaseTime 604905
    Governor gov Proposal LastProposal Execute
    Assert Equal ("Executed") (Governor gov Proposal LastProposal State)

Macro Enfranchise user amount
    From Bank (Comp Transfer user amount)
    From user (Comp Delegate user)

Test "Governor can switch to a new governor"
    DeployGov
    Governor Deploy Alpha Beta (Address Timelock) (Address Comp) Guardian
    Governor Alpha Propose "Upgrade Governor" [(Address Timelock)] [0] ["setPendingAdmin(address)"] [[(Address Beta)]]
    SucceedQueueExecuteLastProposal Alpha
    Assert Equal (Timelock PendingAdmin) (Address Beta)
    From Guardian (Governor Beta Guardian AcceptAdmin)
    Assert Equal (Timelock Admin) (Address Beta)
    Assert Equal (Timelock PendingAdmin) (Address Zero)
    Governor Beta Propose "Add 2" [(Address CNT1)] [0] ["increment(uint256)"] [["2"]]
    SucceedQueueExecuteLastProposal Beta
    Assert Log ProposalExecuted (id 1)
    Assert Equal (Counter CNT1 Count) 2

Test "Guardian can switch to a new governor"
    DeployGov
    Governor Deploy Alpha Beta (Address Timelock) (Address Comp) Guardian
    From Guardian (Governor Alpha Guardian QueueSetTimelockPendingAdmin (Address Beta) 604902)
    IncreaseTime 604905
    From Guardian (Governor Alpha Guardian ExecuteSetTimelockPendingAdmin (Address Beta) 604902)
    From Guardian (Governor Beta Guardian AcceptAdmin)
    Assert Equal (Timelock Admin) (Address Beta)
    Assert Equal (Timelock PendingAdmin) (Address Zero)
    Governor Beta Propose "Add 2" [(Address CNT1)] [0] ["increment(uint256)"] [["2"]]
    IncreaseTime 604901
    From Root (Governor Beta Proposal LastProposal Vote For)
    AdvanceBlocks 20000
    Governor Beta Proposal LastProposal Queue
    Assert Equal ("Queued") (Governor Beta Proposal LastProposal State)
    IncreaseTime 604901
    Governor Beta Proposal LastProposal Execute
    Assert Equal ("Executed") (Governor Beta Proposal LastProposal State)
    Assert Log ProposalExecuted (id 1)
    Assert Equal (Counter CNT1 Count) 2
